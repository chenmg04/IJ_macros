// This macro open multiple ZSeries image folders (acquired by PrairieView from Burker), 
// read and save them as single multipage TIFFs.  In addition, the macro runs z-projection
// for the red channel. 
// There is more information about Bio-Formats Macro Extensions at
//   https://imagej.net/Bio-Formats#Bio-Formats_Macro_Extensions

input  = getDirectory("Input Directory");
output = getDirectory("Output Directory");
list   = getFileList(input);
run("Bio-Formats Macro Extensions");
setBatchMode(true);
for (i = 0; i < list.length; i++){
	    if(startsWith(list[i],"ZSeries")){ 
            name = File.getName(list[i]);
            Ext.openImagePlus(input + list[i] + name + ".xml");
            saveAs("Tiff", input + list[i] + name);  
            run("Split Channels");
            selectWindow("C1-" + name + ".tif");
            run("Z Project...", "projection=[Max Intensity]");
            run("Enhance Contrast", "saturated=0.35");
            run("Apply LUT");
            saveAs("Tiff", output + "MAX_C1-" + name);        
	    } 
	                   
}   
Ext.close(); 

//Stiching, unknown position
run("Grid/Collection stitching", 
"type=[Unknown position] " + 
"order=[All files in directory] " + 
"directory=["+output+"] " + 
"output_textfile_name=TileConfiguration.txt " + 
"fusion_method=[Linear Blending] " + 
"regression_threshold=0.30 " + 
"max/avg_displacement_threshold=2.50 " + 
"absolute_displacement_threshold=3.50 " + 
"computation_parameters=[Save memory (but be slower)] " +
"image_output=[Fuse and display] ");
setBatchMode(false); 

// Under many conditions, images with weak signals could not be registered
// Check the Log or the TileConfiguration.registered.txt file to see which files are registered
// Open the other files and manually stitch them